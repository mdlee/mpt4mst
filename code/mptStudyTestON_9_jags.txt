# MPT old/new study/test
# v9 measurement model
model{
  # Individual parameters
  for (i in 1:nParticipants){
    rho[i] ~ dunif(0, 1)
    gamma[i] ~ dunif(0, 1)
    psi[i] ~ dunif(0, 1)
    beta[i] ~ dnorm(10, 1/10^2)T(0,)
    tau[i] ~ dunif(0, 1)
    for (j in 1:nLures){
      delta[i, j] = 1/(1 + exp(-beta[i]*(j/(nLures+1) - tau[i])))
    }
  }
  # Data
  for (t in 1:nTotalTrials){
    # theta is probability of old response 
    # for 1 = old, 2 = new, 3 = lure stimuli
    theta[t, 1] = rho[p[t]] + (1-rho[p[t]])*gamma[p[t]]
    theta[t, 2] = (1-psi[p[t]])*gamma[p[t]]
    theta[t, 3] = rho[p[t]]*(1-delta[p[t], lureBin[t]]) + (1-rho[p[t]])*gamma[p[t]]
    y[t] ~ dbern(theta[t, truth[t]])
  }
  # Posterior predictive
  for (i in 1:nParticipants){
    pp[i, 1] = rho[i] + (1-rho[i])*gamma[i]
    for (j in 1:nLures){
      pp[i, j+1] = rho[i]*(1-delta[i, j]) + (1-rho[i])*gamma[i]
    }
    pp[i, nLures+2] = (1-psi[i])*gamma[i]
  }
}